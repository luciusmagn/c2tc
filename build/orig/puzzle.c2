module puzzle;

import stdio as io;
import stdlib local;

// TODO support const char* const (for string constants)
const char* ansi_BRED   = "\033[01;31m";
const char* ansi_BGREEN = "\033[01;32m";
const char* ansi_NORMAL = "\033[0m";

type Point enum int32 {
    POINT_A=0, POINT_B, POINT_C, POINT_D, POINT_E, POINT_F,
    POINT_G, POINT_H, POINT_I, POINT_J, POINT_K, POINT_L,
    POINT_M, POINT_N, POINT_O, POINT_P, POINT_Q, POINT_R,
    POINT_MAX,
}

type Connection struct {
    Point src;
    Point dst;
    bool  otherDepth;
}

const Connection[] Connections = {
    { POINT_A, POINT_C, true },
    { POINT_B, POINT_A, true },
    { POINT_C, POINT_E, true }, { POINT_C, POINT_F, false },
    { POINT_D, POINT_A, true },
    { POINT_D, POINT_G, true },
    { POINT_E, POINT_I, false },
    { POINT_F, POINT_H, true },
    { POINT_G, POINT_H, true }, { POINT_G, POINT_I, true },
    { POINT_J, POINT_L, true },
    { POINT_K, POINT_J, true }, { POINT_K, POINT_L, true },
    { POINT_M, POINT_K, true }, { POINT_M, POINT_O, true },
    { POINT_N, POINT_Q, true },
    { POINT_O, POINT_N, true }, { POINT_O, POINT_R, true },
    { POINT_P, POINT_N, true },
    { POINT_Q, POINT_P, true },
    { POINT_R, POINT_B, true },
}

int32 startPoint;
int32 endPoint;
const int32 DEPTH_MAX = 10;
int32[POINT_MAX*DEPTH_MAX] paths;    // value 0 is start, -1 not used, other is prev in path

func int32 index(Point p, int32 depth) { return p + POINT_MAX * depth; }

func Point toPoint(int32 i) {
    //return (Point)(i%POINT_MAX);
    return (i%POINT_MAX);
}

func int32 toDepth(int32 i) { return (i/POINT_MAX); }

func const char* print(int32 index2) {
    local char[16][4] buffer;
    local int32 idx = 0;
    idx = (idx + 1) % 4;
    int32 p = 'A' + toPoint(index2);
    io.sprintf(buffer[idx], "%c%d", p, toDepth(index2));
    return buffer[idx];
}

func void showPaths() {
    io.printf("depth   A   B   C   D   E   F   G   H   I   J   K   L   M   N   O   P   Q   R\n");
    for (int32 d=0; d<DEPTH_MAX; d++) {
        io.printf("%5d   ", d);
        for (int32 p=0; p<POINT_MAX; p++) {
            //int32 idx = index((Point)p, d);
            int32 idx = index(p, d);
            int32 prev = paths[idx];
            if (idx == startPoint) io.printf(ansi_BGREEN);
            if (idx == endPoint) io.printf(ansi_BRED);
            if (prev == -1) io.printf("  ");
            else io.printf("%s", print(prev));
            if (d==0 && (p==POINT_A || p==POINT_J)) io.printf(ansi_NORMAL);
            io.printf("  ");
        }
        io.printf("\n");
    }
}

func void tryPath(int32 cur, int32 next) {
    if (paths[next] == -1) {
        paths[next] = cur;
        listAdd(next);
    }
}

func int32 printPath(int32 idx) {
    int32 prev = paths[idx];
    int32 step = 0;
    if (prev != idx) step = printPath(prev) + 1;
    io.printf("  %2d %s\n", step, print(idx));
    return step;

}

public func int32 main(int32, const int8*[]) {
    // initialize paths
    for (int32 p=0; p<POINT_MAX; p++) {
        for (int32 d=0; d<DEPTH_MAX; d++) {
            paths[index(p,d)] = -1;
            // C2 cannot properly handle CastOperator yet
            //paths[index((Point)p,d)] = -1;
        }
    }
    startPoint = index(POINT_A, 0);
    endPoint = index(POINT_J, 0);
    // add start point
    paths[startPoint] = 0;
    listAdd(startPoint);

    while (listSize()) {
        int32 idx = listGet();
        int32 prev = paths[idx];
        if (prev == -1) {
            io.printf("Error: path has no parent %s\n", print(idx));
            exit(-1);
        }
        Point curPoint = toPoint(idx);
        int32 curDepth = toDepth(idx);

        // follow all Connections, except incoming
        for (uint32 c = 0; c<elemsof(Connections); c++) {
            const Connection* cc = &Connections[c];
            int32 next = -1;
            if (cc.src == curPoint) { // go down (or same)
                next = index(cc.dst, cc.otherDepth ? curDepth + 1 : curDepth);
                if (next == prev) continue;
                tryPath(idx, next);
            } else if (cc.dst == curPoint) { // go up (or same)
                next = index(cc.src, cc.otherDepth ? curDepth - 1 : curDepth);
                if (next == prev) continue;
                tryPath(idx, next);
            }
            if (next == endPoint) { // got destination
                listClear();
                break;
            }
        }
    }
    showPaths();
    if (paths[endPoint] == -1) {
        io.printf("No solution found\n");
    } else {
        io.printf("Found path:\n");
        printPath(endPoint);
    }

    return 0;
}

